---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "transparentedge_vclconf Resource - transparentedge"
subcategory: ""
description: |-
  
---

# transparentedge_vclconf (Resource)



## Example Usage

```terraform
#################
### EXAMPLE 1 ###
#################
# The verbatim VCL code is set here in a heredoc string: https://developer.hashicorp.com/terraform/language/expressions/strings#heredoc-strings
# if you already have a VCL configuration it's recommended to import it directly from the dashboard.
variable "code" {
  type    = string
  default = <<EOF
sub vcl_recv {
    if (req.http.host == "www.example.com") {
        set req.backend_hint = cX_myorigin.backend();
        set req.http.TCDN-i3-transform = "auto_webp";
        set bereq.http.TCDN-Command = "redirect_https, brotli_compress";
    }
}
EOF
}

resource "transparentedge_vclconf" "prod" {
  vclcode = var.code
}

output "prod_config" {
  value = transparentedge_vclconf.prod
}

#################
### EXAMPLE 2 ###
#################
# You can also set the heredoc string directly on the resource or reference an external file:
resource "transparentedge_vclconf" "external_file" {
  vclcode = file("${path.module}/config.vcl")
}

#################
### EXAMPLE 3 ###
#################
# If you keep the configuration inside of a tf file you'll be able to reference other variables.
# This is a good approach since the backends are tied to the configuration
resource "transparentedge_backend" "myorig" {
  name   = "origin1"
  origin = "my-origin.com"
  port   = 443
  ssl    = true

  # healthcheck
  hchost       = "www.my-origin.com"
  hcpath       = "/favicon.ico"
  hcstatuscode = 200
}

resource "transparentedge_vclconf" "backend_dependency" {
  vclcode = <<EOF
sub vcl_recv {
    if (req.http.host == "www.example.com") {
        set req.backend_hint = ${resource.transparentedge_backend.myorig.vclname}.backend();
    }
}
EOF
}

# Another option would be to use template files:
# https://developer.hashicorp.com/terraform/language/functions/templatefile
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `vclcode` (String) Verbatim of the VCL (_Varnish Configuration Language_) code configuration to apply. After a successful code upload, it may take between 5 and 10 minutes for the new configuration to be fully replicated in all the CDN edge nodes. You can check if a configuration is already in production by running `terraform plan` and checking the `productiondate` field.

### Read-Only

- `company` (Number) Company ID that owns this VCL config
- `id` (Number) ID of the VCL Config
- `productiondate` (String) Date when the configuration was fully applied in the CDN
- `uploaddate` (String) Date when the configuration was uploaded
- `user` (String) User that created the configuration

## Import

Import is supported using the following syntax:

```shell
# Import the active VCL configuration, the ID value doesn't matter.
terraform import 'transparentedge_vclconf.prod' 0

# Importing VCL code has its quirks, since the API parses the code and
# add/removes newlines and spaces the diff won't be equal, it's usually
# better to just copy the last configuration from the dashboard and
# apply again.
```
